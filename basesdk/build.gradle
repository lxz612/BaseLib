apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // ---- 官方support库 ----
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:multidex:1.0.3'
    compile 'com.android.support.constraint:constraint-layout:1.1.0'
    compile 'com.android.support:support-annotations:27.1.1'

    // -------- 三方库 --------
    // 路由库
    compile 'com.alibaba:arouter-api:1.3.1'
    compile "com.alibaba:arouter-annotation:1.0.4"

    // 网络库
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.squareup.retrofit2:retrofit:2.4.0'

    // 图片加载库
    compile 'com.github.bumptech.glide:glide:4.3.0'

    // 总线
    compile 'org.greenrobot:eventbus:3.1.1'

    // Json解析
    compile 'com.google.code.gson:gson:2.8.0'

    // 动态权限
    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0'

    // 日志
    compile 'com.orhanobut:logger:1.8'

    api 'me.imid.swipebacklayout.lib:library:1.1.0'
    // 下拉刷新
    api 'in.srain.cube:ultra-ptr:1.0.11'
}

// -------------------------------------- 上传到JCenter ---------------------------------------------
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "1.0.6"                                         // 每次发布都应该设置版本

def siteURL = 'https://github.com/lxz612/BaseLib.git'     // 项目的主页  这个是说明，可随便填
def gitURL = 'https://github.com/lxz612/BaseLib.git'      // Git仓库的url  这个是说明，可随便填
group = "com.lxz.baselib"                                 // 这里是groupId  必须填写 一般填你唯一的包名，对应com.squareup.okhttp3:okhttp:3.4.1中的com.squareup.okhttp3部分

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'BaseSDK'
                url siteURL
                // Set your license
                licenses {
                    license {
                        name 'The BaseSDK Software License, Version 1.0.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        // 填写开发者的一些基本信息
                        id 'lxz612'
                        name 'lxz612'
                        email 'lxz612@outlook.com'
                    }
                }
                scm {
                    connection gitURL
                    developerConnection gitURL
                    url siteURL
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    // 读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   // 读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "basesdk"
        // 发布到JCenter上的项目名字，必须填写，对应com.squareup.okhttp3:okhttp:3.4.1中的okhttp
        websiteUrl = siteURL
        vcsUrl = gitURL
        licenses = ["Apache-2.0"]
        publish = true
    }
}
